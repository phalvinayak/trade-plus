// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© phalvinayak

//@version=5

indicator('Custom-OHLC', shorttitle='Custom-OHLC', format=format.price, precision=2, overlay=true)

// Yesterday OHLC plot on the chart
// ****************************************************************************************
string periodLengthInput = input.string(defval="1 day", title='MA', options=["1 minute", "3 minutes", "1 day", "1 week", "1 month"], group='OHLC Settings', inline='OHLC Settings')
color ohlcColorInput = input.color(#FD0E35, '', group='OHLC Settings', inline='OHLC Settings')
customTime = switch periodLengthInput
    "1 minute" => "1"
    "3 minutes" => "3"
    "1 day" => "D"
    "1 week" => "W"
    "1 month" => "M"
customLabel = switch periodLengthInput
    "1 minute" => "1m"
    "3 minutes" => "3m"
    "1 day" => "1D"
    "1 week" => "1W"
    "1 month" => "1M"
[yDayOpen, yDayHigh, yDayLow, yDayClose, yDayTime, yBarIndex] = request.security(syminfo.tickerid, customTime, [open[1], high[1], low[1], close[1], time[1], bar_index[1]], lookahead = barmerge.lookahead_on)

todayTime = request.security(syminfo.tickerid, "D", time, gaps=barmerge.gaps_off, lookahead=barmerge.lookahead_on)
today = timestamp(syminfo.timezone, year(todayTime), month(todayTime), dayofmonth(todayTime), 09, 15)
day_ms = 86400000
labelXPos = time + (20 * (time - time[1]))

f_newLine(_c, _s, _w) => line.new(na, na, na, na, xloc.bar_time, extend.right, _c, _s, _w)
f_newLabel(labelText) => label.new(na, na, labelText, xloc.bar_time, style=label.style_none, textcolor=ohlcColorInput)
f_moveLine(_line, _x, _y) =>
    line.set_xy1(_line, _x, _y)
    line.set_xy2(_line, _x + day_ms * 30, _y)
    line.set_extend(_line, extend.right)
f_moveLabel(_label, _x, _y) =>
    label.set_xy(_label, _x, _y)
    label.set_textalign(_label, textalign = text.align_right)

var line openLine = f_newLine(ohlcColorInput, line.style_solid, 1)
f_moveLine(openLine, yDayTime, yDayOpen)
var label openLabel = f_newLabel("O-" + customLabel)
f_moveLabel(openLabel, labelXPos, yDayOpen)

var line closeLine = f_newLine(ohlcColorInput, line.style_solid, 1)
f_moveLine(closeLine, yDayTime, yDayClose)
var label closeLabel = f_newLabel("C-" + customLabel)
f_moveLabel(closeLabel, labelXPos, yDayClose)

var line highLine = f_newLine(ohlcColorInput, line.style_solid, 1)
f_moveLine(highLine, yDayTime, yDayHigh)
var label highLabel = f_newLabel("H-" + customLabel)
f_moveLabel(highLabel, labelXPos, yDayHigh)

var line lowLine = f_newLine(ohlcColorInput, line.style_solid, 1)
f_moveLine(lowLine, yDayTime, yDayLow)
var label lowLabel = f_newLabel("L-" + customLabel)
f_moveLabel(lowLabel, labelXPos, yDayLow)
// ****************************************************************************************